; This sourcecode published under the Terms of the General Public License.
; Written by Eric Esser, Wissenschaftszentrum Berlin, eric.esser@wzb.eu.
; Inspired by uib gmbh (www.uib.de), Thomas Besser (KIT), Detlef Krummel (GEI) and Tobias Friede (Fraunhofer)

[Actions]
;==========================================================================================================
; Includes
include_insert "%ScriptPath%\common.opsiinc"

;==========================================================================================================
; Variables 
DefVar $CustomPostInstall$
DefVar $SetupString$
DefVar $SetupStringDeutsch$

Set $SetupString$ = "%ScriptPath%" + "\" +  $SetupFile$
Set $SetupStringDeutsch$ = "%ScriptPath%" + "\" +  $SetupFileDeutsch$
;==========================================================================================================
; Program logic

comment "Show product picture"
ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$ + " " + $Version$
Message "Installing " + $ProductId$ + " ..."

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "Not enought space on %SystemDrive%"
	; Stop process and set installation status to failed
endif

if ((GetRegistryStringValue64("["+$ActVersionRegPath$+"] DisplayName") = "") or (getProductProperty("forcereinstall","False") = "True"))	
  if FileExists($SetupString$)
		comment "Start setup program"
	  ChangeDirectory "%SCRIPTPATH%"
    Winbatch_install
	  Sub_check_exitcode
  	ExitWindows /Reboot
	else
	  LogError "No setup file found, please check the OPSI package"
	  isFatalError "No Setup file found"
	endif
else
  comment $PrettyName$ + " " + $Version$ + " already installed. No forced reinstall selected, thus skipping installation"
  message $PrettyName$ + " " + $Version$ + " already installed. No forced reinstall selected, thus skipping installation"
endif

if ((GetRegistryStringValue64("["+$ActVersionRegPathDeutsch$+"] DisplayName") = "") or (getProductProperty("forcereinstall","False") = "True"))	
  if ((getProductProperty("german-language-pack","auto") = "True") or ((getProductProperty("german-language-pack","auto") = "auto") and ($system_language$ = "0407") ) )
	  Set $SetupString$ = "%ScriptPath%" + "\" + $SetupFileDeutsch$
    if FileExists($SetupString$)
		  comment "Installing German language pack"
		  message "Installing German language pack"
	    ChangeDirectory "%SCRIPTPATH%"
      Winbatch_install
	    Sub_check_exitcode
	    ExitWindows /Reboot
	  else
	    LogError "No German language pack file found, please check the OPSI package"
	    isFatalError "No German language pack file found"
	  endif
	endif
else
  comment $PrettyName$ + " " + $Version$ + " German language pack already installed. No forced reinstall selected, thus skipping installation"
  message $PrettyName$ + " " + $Version$ + " German language pack  already installed. No forced reinstall selected, thus skipping installation"
endif

comment "include custom post install file"
set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
if not ($CustomPostInstall$ = "none")
  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
    include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
  endif
endif

[Winbatch_install]
"$SetupString$" /passive /norestart /log "$LogDir$\$ProductId$.install_log.txt"
